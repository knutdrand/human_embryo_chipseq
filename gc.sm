names = ["M", "GV", "IVF", "ICSI", "Day3G", "Day2G", "Zygote", "BlastG", "Ooc2", "preZGA"]
regiontypes = ["domains_minus_tss500", "non_tss_containing_domains", "domain_flanks",
                           "tss_containing_domains"]
rule get_tss:
    input:
        "data/{species}_genes.bed"
    output:
        "{species}/regions/tss/1.bed"
    shell:
        """awk '{{OFS="\t"}}{{if ($6=="+") {{print $1,$2,$2+1}} else {{print $1, $3-1, $3}}}}' {input} > {output}"""

rule get_tss_500:
    input:
        "{species}/regions/tss/1.bed",
        "data/{species}.chrom.sizes"
    output:
        "{species}/regions/tss/500.bed"
    shell:
        "bedtools slop -i {input[0]} -g {input[1]} -b 500 > {output}"

rule get_domain_flanks:
    input:
        "{species}/{version}/domains/{name}.bed",
        "data/{species}.chrom.sizes"
    output:
        "{species}/{version}/regions/domain_flanks/{name}.bed"
    shell:
        "bedtools flank -i {input[0]} -g {input[1]}  -b 5000 | awk '{{if ($3>$2) print}}'  > {output}"

rule get_tss_containing_domains:
    input:
        "{species}/{version}/domains/{name}.bed",
        "{species}/regions/tss/1.bed"
    output:
        "{species}/{version}/regions/tss_containing_domains/{name}.bed"
    shell:
        """bedtools intersect -a {input[0]} -b {input[1]} -c | awk '{{if ($NF>0) print}}' > {output}"""

rule get_non_tss_containing_domains:
    input:
        "{species}/{version}/domains/{name}.bed",
        "{species}/regions/tss/1.bed"
    output:
        "{species}/{version}/regions/non_tss_containing_domains/{name}.bed"
    shell:
        "bedtools intersect -a {input[0]} -b {input[1]} -c | awk '{{if ($NF==0) print}}' > {output}"

rule get_tss_containing_domains_minus_tss500:
    input:
        "{species}/{version}/regions/tss_containing_domains/{name}.bed",
        "{species}/regions/tss/500.bed"
    output:
        "{species}/{version}/regions/domains_minus_tss500/{name}.bed"
    shell:
        "bedtools subtract -a {input[0]} -b {input[1]} > {output}"

rule get_gc:
    input:
        "../../Data/{species}.clean.fna",
        "{species}/{version}/regions/{folder}/{name}.bed"
    output:
        "{species}/{version}/regions/{folder}/gc/{name}.txt"
    shell:
        "bedtools nuc -fi {input[0]} -bed {input[1]} | grep -v chrM | awk '{{gc+=$(NF-4)+$(NF-5);t+=$NF}}END{{print gc/t}}' > {output}"

rule summarize_gc:
    input:
        expand("{{species}}/{{version}}/regions/{regiontype}/gc/{{name}}.txt",
               regiontype=regiontypes
               )
    output:
        "{species}/{version}/gc_content/{name}.txt"
    shell:
        'paste {input} <(echo "{wildcards.name}") > {output}'

rule all_gc:
    input:
        expand("{{species}}/{{version}}/gc_content/{name}.txt",
               name=names)
    output:
        "{species}/{version}/gc_content/ALL.tsv"
    shell:
        """
        echo "domains_minus_tss500\tnon_tss_containing_domains\tdomain_flanks\ttss_containing_domains\tname" > {output}
        cat {input} >> {output}
        """
